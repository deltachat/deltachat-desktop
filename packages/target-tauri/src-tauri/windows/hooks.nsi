; nsis hooks to extend the installer that is generated by tauri using the nsis installer framework
; https://tauri.app/distribute/windows-installer/#extending-the-installer

; defines a macro to register as a handler for an uri scheme
; adapted from https://github.com/tauri-apps/tauri/blob/f0662e41f4f78ec5a3f88aa76a7367d37d740291/crates/tauri-bundler/src/bundle/windows/nsis/installer.nsi#L648
!macro URI_SCHEME protocol
    WriteRegStr SHCTX "Software\Classes\${protocol}" "URL Protocol" ""
    WriteRegStr SHCTX "Software\Classes\${protocol}" "" "URL:${BUNDLEID} protocol"
    WriteRegStr SHCTX "Software\Classes\${protocol}\DefaultIcon" "" "$\"$INSTDIR\${MAINBINARYNAME}.exe$\",0"
    WriteRegStr SHCTX "Software\Classes\${protocol}\shell\open\command" "" "$\"$INSTDIR\${MAINBINARYNAME}.exe$\" $\"%1$\""
!macroend

; defines a macro to remove as handler for an uri scheme
; adapted from https://github.com/tauri-apps/tauri/blob/f0662e41f4f78ec5a3f88aa76a7367d37d740291/crates/tauri-bundler/src/bundle/windows/nsis/installer.nsi#L781
!macro REMOVE_URI_SCHEME protocol
    ReadRegStr $R7 SHCTX "Software\Classes\${protocol}\shell\open\command" ""
    ${If} $R7 == "$\"$INSTDIR\${MAINBINARYNAME}.exe$\" $\"%1$\""
      DeleteRegKey SHCTX "Software\Classes\${protocol}"
    ${EndIf}
!macroend


;  Runs before copying files, setting registry key values and creating shortcuts.
!macro NSIS_HOOK_PREINSTALL
;   MessageBox MB_OK "PreInstall"
!macroend

; Runs after the installer has finished copying all files, setting the registry keys and created shortcuts.
!macro NSIS_HOOK_POSTINSTALL
;   MessageBox MB_OK "PostInstall"
    ; this macro is defined in https://github.com/tauri-apps/tauri/blob/85b19125294917e10e89fc9e09722eaaa4f69962/crates/tauri-bundler/src/bundle/windows/nsis/FileAssociation.nsh
    !insertmacro APP_ASSOCIATE "xdc" "chat.delta.tauri.webxdc" "open" "$INSTDIR\xdc.ico" "Share App with ${PRODUCTNAME}" "$INSTDIR\deltachat-tauri.exe $\"%1$\""

    !insertmacro URI_SCHEME "openpgp4fpr"
    !insertmacro URI_SCHEME "dcaccount"
    !insertmacro URI_SCHEME "dclogin"
    !insertmacro URI_SCHEME "dcnotification"
!macroend

; Runs before removing any files, registry keys and shortcuts.
!macro NSIS_HOOK_PREUNINSTALL
;   MessageBox MB_OK "PreUnInstall"
    !insertmacro APP_UNASSOCIATE "xdc" "chat.delta.tauri.webxdc"

    !insertmacro REMOVE_URI_SCHEME "openpgp4fpr"
    !insertmacro REMOVE_URI_SCHEME "dcaccount"
    !insertmacro REMOVE_URI_SCHEME "dclogin"
    !insertmacro REMOVE_URI_SCHEME "dcnotification"
!macroend

; Runs after files, registry keys and shortcuts have been removed.
!macro NSIS_HOOK_POSTUNINSTALL
;   MessageBox MB_OK "PostUninstall"
!macroend
